buildscript {
   ext {
      jacksonVersion = '2.10.3'
      jsonPathVersion = '2.4.0'
      httpClientVersion = '4.5.12'
      logbackVersion = '1.2.3'
      jupiterVersion = '5.6.2'
   }
}

plugins {
   id 'java'
   id 'maven-publish'
   id 'signing'
}

group 'club.javafamily'
version '0.1.1'

sourceCompatibility = 1.8

repositories {
   jcenter()
   mavenCentral()
}

dependencies {
   implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
   implementation("com.jayway.jsonpath:json-path:${jsonPathVersion}") {
      exclude(group: 'org.slf4j', module: 'slf4j-api')
   }

   implementation("org.apache.httpcomponents:httpclient:${httpClientVersion}") {
      exclude(group: 'commons-logging', module: 'commons-logging')
   }

   implementation "ch.qos.logback:logback-classic:${logbackVersion}"

   testImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
}

task sourcesJar(type: Jar) {
   from sourceSets.main.allSource
   archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
   from javadoc
   archiveClassifier = 'javadoc'
}

publishing {
   // 定义发布什么
   publications {
      mavenJava(MavenPublication) {
         groupId = project.group
         artifactId = project.name
         version = "${version}"
         from components.java
         artifact sourcesJar
         artifact javadocJar
         pom {
            // 构件名称
            // 区别于artifactId，可以理解为artifactName
            name = 'JavaFamily Utils'
            // 构件描述
            description = 'JavaFamily Utils library'
            // 构件主页
            url = 'https://github.com/JavaFamilyClub/javafamily-utils'
            // 许可证名称和地址
            licenses {
               license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
               }
            }
            // 开发者信息
            developers {
               developer {
                  id = 'JavaFamilyClub'
                  name = 'JavaFamily'
                  email = 'javafamily.club@outlook.com'
                  organization = 'JavaFamily'
               }
            }
            // 版本控制仓库地址
            scm {
               url = 'https://github.com/JavaFamilyClub/javafamily-utils'
               connection = 'scm:git:git://github.com/JavaFamilyClub/javafamily-utils.git'
               developerConnection = 'scm:git:ssh://git@github.com/JavaFamilyClub/javafamily-utils.git'
            }
         }
      }
   }
   // 定义发布到哪里
   repositories {
      maven {
         def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
         def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

         url = project.version.endsWith('-SNAPSHOT') ? snapshotsUrl : releasesUrl
         credentials {
            username sonatypeUsername
            password sonatypePassword
         }
      }
   }
}

signing {
   sign publishing.publications.mavenJava
}

javadoc {
   options.addStringOption("charset", "UTF-8")
}

test {
   useJUnitPlatform()
   testLogging {
      events "passed", "skipped", "failed"
   }
}

wrapper {
   gradleVersion '6.2.1'
}
